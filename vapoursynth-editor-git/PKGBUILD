pkgname=vapoursynth-editor-git
pkgver=r19.60.g503820d
pkgrel=1
pkgdesc="A simple program for edit/create VapourSynth scripts. (GIT version)"
arch=('x86_64')
url='https://github.com/sandsmark/vapoursynth-editor'
license=('CCPL' 'MIT' 'LGPL')
depends=('qt5-base'
         'qt5-websockets'
         'vapoursynth'
         )
makedepends=('git')
provides=('vapoursynth-editor')
conflicts=('vapoursynth-editor')
source=('git+https://github.com/sandsmark/vapoursynth-editor.git'
        'vsedit_raise_snapshot_png_compress_level.patch'
        'vsedit.desktop'
        'vsedit_server_watch.desktop'
        )
sha256sums=('SKIP'
            '17846a6cf651ca1b9adca0244a45ea465a19e6b4b9c0145ab1f875139e7f5a68'
            '7c73f873a970bb295be8fdcf8ef570ef9bb812232b3cc2dce79b42c57c742623'
            'bc6443a2dfa48ee1e2156039be2195074dfd4a02f5f1696f8406c715a1b19ccf'
            )

pkgver() {
  cd vapoursynth-editor
  echo "$(git describe --long --tags | tr - .)"
}

prepare() {
  cd vapoursynth-editor
  patch -p1 < ../vsedit_raise_snapshot_png_compress_level.patch
  mkdir -p build && cd build
  cmake -DCMAKE_BUILD_TYPE="Release" ..
}

build() {
  cd vapoursynth-editor/build
  make
}

package() {
  install -Dm644 vsedit.desktop "${pkgdir}/usr/share/applications/vsedit.desktop"
  install -Dm644 vsedit_server_watch.desktop "${pkgdir}/usr/share/applications/vsedit_server_watch.desktop"

  cd "vapoursynth-editor/build"
  install -Dm755 vsedit "${pkgdir}/usr/bin/vsedit"
  install -Dm755 vsedit-job-server "${pkgdir}/usr/bin/vsedit-job-server"
  install -Dm755 vsedit-job-server-watcher "${pkgdir}/usr/bin/vsedit-job-server-watcher"
  
  cd ..
  install -Dm644 resources/vsedit.svg "${pkgdir}/usr/share/pixmaps/vsedit.svg"
  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  install -Dm644 README "${pkgdir}/usr/share/doc/vsedit/README"
}
