pkgname=x265-Asuna-mod
pkgver=3.2
pkgrel=1
pkgdesc='Open Source H265/HEVC video encoder'
arch=('x86_64')
url='https://github.com/msg7086/x265-Yuuki-Asuna'
license=('GPL')
depends=('gcc-libs'
          'numactl'
          'libavcodec.so'
          'libavformat.so'
          'libavutil.so'
          'liblsmash.so')
makedepends=('cmake' 'git' 'nasm' 'ruby')
source=('git+https://github.com/msg7086/x265-Yuuki-Asuna.git'
        'CMakeLists.txt.patch')
sha256sums=('SKIP'
            'ae72f5374bdae8b6f0c11cbf840cec3e97d58a2affa49629d326bad34d5f509d')

prepare() {
  cd x265-Yuuki-Asuna
  git checkout Asuna

  patch -p1 < ../CMakeLists.txt.patch
  for d in 8 10 12; do
    if [[ -d build-$d ]]; then
      rm -rf build-$d
    fi
    mkdir build-$d
  done
}

build() {
  cd x265-Yuuki-Asuna/build-12

  cmake ../source \
    -DCMAKE_INSTALL_PREFIX='/usr' \
    -DHIGH_BIT_DEPTH='TRUE' \
    -DMAIN12='TRUE' \
    -DEXPORT_C_API='FALSE' \
    -DENABLE_CLI='FALSE' \
    -DENABLE_SHARED='FALSE' \
    -DENABLE_LAVF='TRUE' \
    -DENABLE_LSMASH='TRUE' \
    -DENABLE_MKV='TRUE' \
    -DENABLE_AVISYNTH='FALSE' \
    -DENABLE_STATIC_LAVF='TRUE' \
    -DCMAKE_CXX_STANDARD=11
  make

  cd ../build-10

  cmake ../source \
    -DCMAKE_INSTALL_PREFIX='/usr' \
    -DHIGH_BIT_DEPTH='TRUE' \
    -DEXPORT_C_API='FALSE' \
    -DENABLE_CLI='FALSE' \
    -DENABLE_SHARED='FALSE' \
    -DENABLE_LAVF='TRUE' \
    -DENABLE_LSMASH='TRUE' \
    -DENABLE_MKV='TRUE' \
    -DENABLE_AVISYNTH='FALSE' \
    -DENABLE_STATIC_LAVF='TRUE' \
    -DCMAKE_CXX_STANDARD=11
  make

  cd ../build-8

  ln -s ../build-10/libx265.a libx265_main10.a
  ln -s ../build-12/libx265.a libx265_main12.a

  cmake ../source \
    -DCMAKE_INSTALL_PREFIX='/usr' \
    -DENABLE_SHARED='FALSE' \
    -DENABLE_HDR10_PLUS='TRUE' \
    -DEXTRA_LIB='x265_main10.a;x265_main12.a' \
    -DEXTRA_LINK_FLAGS='-L .' \
    -DSTATIC_LINK_CRT='TRUE' \
    -DEXPORT_C_API='TRUE' \
    -DLINKED_10BIT='TRUE' \
    -DLINKED_12BIT='TRUE' \
    -DENABLE_LAVF='TRUE' \
    -DENABLE_LSMASH='TRUE' \
    -DENABLE_MKV='TRUE' \
    -DENABLE_AVISYNTH='FALSE' \
    -DENABLE_STATIC_LAVF='TRUE' \
    -DCMAKE_CXX_STANDARD=11
  make
}

package() {
  cd x265-Yuuki-Asuna/build-8

  make DESTDIR="${pkgdir}" install
  rm -rf ${pkgdir}/usr/include
  rm -rf ${pkgdir}/usr/lib
  mv ${pkgdir}/usr/bin/x265 ${pkgdir}/usr/bin/x265-Asuna
}
