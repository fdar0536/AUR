pkgname=aegisub-japan7-git
pkgver=3.3.3.r73.09b424fbb
pkgrel=1
pkgdesc='A fork of aegisub including tap-to-time feature and custom user scripts'
arch=(x86_64)
url=https://github.com/odrling/Aegisub
license=(
  custom
)
depends=(
  alsa-lib
  amaranth-font
  boost-libs
  fftw
  fontconfig
  hunspell
  icu
  libass.so
  libffms2.so
  libgl
  libpulse
  openssl
  pango
  uchardet
  wxgtk3
  zlib
)
makedepends=(
  autoconf-archive
  boost
  git
  intltool
  lua
  mesa
  meson
)
provides=(aegisub)
conflicts=(aegisub)
source=(
  aegisub::git+https://github.com/odrling/Aegisub.git#branch=meson
  'fix_text_pos.patch'
)
sha256sums=(
  SKIP
  'eca30590e701871fb8a1cc415e906425dcd436a7526e50ebecd9cd9b55b5efb9'
)

pkgver() {
  cd aegisub

  tag='v3.3.3'

  echo "${tag#v}.r$(git rev-list --count ${tag}..HEAD).$(git rev-parse --short HEAD)"
}

prepare() {
  cd aegisub

  patch -p1 < ../fix_text_pos.patch
  arch-meson builddir -Dportaudio=disabled -Dopenal=disabled
}

build() {
  cd aegisub

  ninja -C builddir
}

package() {
  cd aegisub

  DESTDIR="${pkgdir}" ninja -C builddir install
  install -Dm 644 LICENCE -t "${pkgdir}"/usr/share/licenses/aegisub-git/
}

# vim: ts=2 sw=2 et:
